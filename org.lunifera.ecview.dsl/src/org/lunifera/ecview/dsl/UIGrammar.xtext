grammar org.lunifera.ecview.dsl.UIGrammar with org.eclipse.xtext.xbase.Xbase

import "platform:/resource/org.eclipse.emf.ecp.ecview.common.model/model/core.ecore" as core
import "platform:/resource/org.eclipse.emf.ecp.ecview.common.model/model/validation.ecore" as coreVal
import "platform:/resource/org.eclipse.emf.ecp.ecview.extension.model/model/extension.ecore" as ext
import "platform:/resource/org.lunifera.ecview.semantic.uisemantics/model/UISemanticsGrammar.ecore" as cfg
import "platform:/resource/org.lunifera.ecview.semantic.uimodel/model/UIGrammarCore.ecore" as grammarCore
import "platform:/resource/org.lunifera.ecview.semantic.uimodel/model/UIGrammarExtensions.ecore"
import "http://www.eclipse.org/xtext/common/JavaVMTypes"

UiModel returns grammarCore::UiModel:
	roots+=UiRootElements*;

UiRootElements returns grammarCore::UiRootElements:
	UiView | UiViewSet | UiVisibilityProcessor;

UiVisibilityProcessor returns UiVisibilityProcessor:
	'visibilityProcessor' name=ID '{'
	bindingAlias+=UiBindingEndpointAlias*
	changeTrigger+=UiChangeTrigger*
	rule=UiVisibilityRule
	'}';

UiChangeTrigger returns UiChangeTrigger:
	'trigger' (endpoint=UiBindingEndpointDef | endpointAlias=[grammarCore::UiBindingEndpointAlias]);

UiVisibilityRule returns UiVisibilityRule:
	UiXbaseVisibilityRule;

UiXbaseVisibilityRule returns UiXbaseVisibilityRule:
	{UiXbaseVisibilityRule}
	'rule' '{'
//	expression=XExpression
	'}';

UiViewSet returns grammarCore::UiViewSet:
	'viewset' name=ID '{'
	(('beanslots' '{' (beanSlots+=UiBeanSlot)* '}')?
	(bindingEndpointAlias+=UiBindingEndpointAlias)*)
	'}';

UiBindingEndpointAlias returns grammarCore::UiBindingEndpointAlias:
	'bindingAlias' endpoint=UiBindingEndpointDef 'as' alias=ID;

UiBeanSlot returns grammarCore::UiBeanSlot:
	'slot' name=ID ':' jvmType=JvmTypeReference;

UiBinding returns grammarCore::UiBinding:
	'bind' (source=UiBindingEndpointDef | sourceAlias=[grammarCore::UiBindingEndpointAlias]) ((targetToSource?='<')? '--'
	(sourceToTarget?='>')?)
	(target=UiBindingEndpointDef | targetAlias=[grammarCore::UiBindingEndpointAlias]);

UiBindingEndpointDef returns grammarCore::UiBindingEndpointDef:
	bindable=[core::YElement|ID] ':' endpoint=[cfg::UxEndpointDef] (path=UiPathSegment)?;

UiPathSegment returns grammarCore::UiPathSegment:
	'.' jvmField=[JvmField] path=(UiPathSegment)?;

UiView returns grammarCore::UiView:
	UiIDEView;

UiIDEView returns grammarCore::UiIDEView:
	'ideview' name=ID
	'{'
	('viewset' root=[grammarCore::UiViewSet])?
	('beanslots' '{' (beanSlots+=UiBeanSlot)* '}')?
	(content=UiLayout)?
	// TODO make unordered group
	('bindingAlias' '{' (bindingEndpointAlias+=UiBindingEndpointAlias)* '}')?
	(bindings+=UiBinding)*
	'}';

UiGridLayout returns ext::YGridLayout:
	{UiGridLayout}
	'gridlayout'
	('('
	((('columns=' columns=INT)?)
	&
	((fillHorizontal?='fill-h')?)
	&
	((fillVertical?='fill-v')?)
	&
	((spacing?='spacing')?)
	&
	((margin?='margin')?))
	')')?
	name=ID?
	'{'
	(contents+=UiGridLayoutAssigment)*
	'}';

UiGridLayoutAssigment:
	element=UiEmbeddable (('align' alignment=UiAlignment)? & ('span' from=UiPoint 'to' to=UiPoint)?);

UiPoint returns grammarCore::UiPoint:
	x=INT '/' y=INT;

enum UiAlignment returns ext::YAlignment:
	BOTTOM_LEFT="bottom left" | BOTTOM_CENTER="bottom center" | BOTTOM_RIGHT="bottom right" | BOTTOM_FILL="bottom fill" |
	MIDDLE_LEFT="middle left" | MIDDLE_CENTER="middle center" | MIDDLE_RIGHT="middle right" | BOTTOM_FILL="middle fill" |
	TOP_LEFT="top left" | TOP_CENTER="top center" | TOP_RIGHT="top right" | TOP_FILL="top fill" |
	FILL_LEFT="fill left" | FILL_CENTER="fill center" | FILL_RIGHT="fill RIGHT" | FILL_FILL="fill fill" |
	UNDEFINED="undefined";

UiSpanInfo returns ext::YSpanInfo:
	{ext::YSpanInfo}
	((('columnFrom' columnFrom=INT)
	&
	('columnTo' columnTo=INT))?
	&
	(('rowFrom' rowFrom=INT)
	&
	('rowTo' rowTo=INT))?);

UiTextField returns UiTextField:
	{UiTextField}
	'textfield' name=ID?
	('{'
	(validators+=UiValidator)*
	'}')?;

UiList returns ext::YList:
	{UiList}
	'list' name=ID?;

enum SelectionType returns ext::YSelectionType:
	SINGLE="SINGLE" | MULTI="MULTI";

UiEmbeddable returns core::YEmbeddable:
	UiLayout | UiField;

UiLayout returns core::YLayout:
	UiGridLayout;

UiField returns core::YField:
	UiTextField | UiList | UiNumericField | UiCheckBox;

UiNumericField returns UiNumericField:
	{UiNumericField}
	'numericField' name=ID?
	('{'
	(validators+=UiValidator)*
	'}')?;

UiCheckBox returns UiCheckBox:
	{UiCheckBox}
	'checkbox' name=ID?
	('{'
	(validators+=UiValidator)*
	'}')?;

UiValidator returns coreVal::YValidator:
	UiMaxLengthValidator | UiMinLengthValidator | UiRegexpValidator;

UiMaxLengthValidator returns grammarCore::UiMaxLengthValidator:
	{grammarCore::UiMaxLengthValidator}
	'maxLengthValidator' name=ID ('maxLength' maxLength=INT)?;

UiMinLengthValidator returns grammarCore::UiMinLengthValidator:
	{grammarCore::UiMinLengthValidator}
	'minLengthValidator' name=ID ('minLength' minLength=INT)?;

UiRegexpValidator returns grammarCore::UiRegexpValidator:
	{grammarCore::UiRegexpValidator}
	'regexValidator' name=ID ('regex' regExpression=STRING)?;

FQN:
	(ID '.')* ID;
