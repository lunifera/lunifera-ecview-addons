grammar org.lunifera.ecview.dsl.UIGrammar with org.eclipse.xtext.xbase.Xbase

import "http://www.lunifera.org/ecview/dsl/uimodel/semantics" as cfg
import "http://www.lunifera.org/ecview/dsl/uimodel/core"
import "http://www.eclipse.org/xtext/common/JavaVMTypes"
import "http://www.eclipse.org/xtext/xbase/Xbase"
import "http://www.eclipse.org/xtext/xbase/Xtype"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

UiModel returns UiModel:
	'package' packageName=QualifiedName
	imports+=UiImports*
	roots+=UiRootElements*;

UiImports returns UiImports:
	'import' importedNamespace=QualifiedNameWithWildcard (';')?;

UiRootElements returns UiRootElements:
	UiView | UiViewSet | UiValidatorAlias | UiMobileView;

UiValidatorAlias returns UiValidatorAlias:
	'validatorAlias' validator=UiValidator 'as' alias=ID (';')?;

UiValidatorAssignment returns UiValidatorAssignment:
	'fieldValidation' field=[UiField] '+=' (validatorDef=UiValidatorDef | validatorAlias=[UiValidatorAlias|QualifiedName]
	(';')?);

UiValidatorDef returns UiValidatorDef:
	validator=UiValidator;

UiViewSet returns UiViewSet:
	'viewset' name=ID '{'
	(beanSlots+=UiBeanSlot)*
	(bindingEndpointAlias+=UiBindingEndpointAlias)*
	'}';

UiBindingEndpointAlias returns UiBindingEndpointAlias:
	'dataAlias' endpoint=UiBindingEndpointAssignment 'as' alias=ID (';')?;

UiBeanSlot returns UiBeanSlot:
	'datasource' name=ID ':' jvmType=JvmTypeReference (';')?;

UiBinding returns UiBinding:
	'bind' (listBinding?='list')? source=UiBindingEndpointAssignment ((targetToSource?='<')? '--'
	(sourceToTarget?='>')?) target=UiBindingEndpointAssignment (';')?;

UiBindingEndpointAssignment returns UiBindingExpression:
	((UiTypedBindableDef {UiBindingEndpointAssignment.typedBindableDef=current} path=(UiPathSegment)?)
	| (UiCommandBindableDef {UiBindingEndpointAssignment.typedBindableDef=current})
	| ({UiBindingEndpointAssignment} typedBindableAlias=[UiTypedBindable] path=(UiPathSegment)?));

UiPathSegment returns UiPathSegment:
	{UiPathSegment}
	'.' getter=[JvmOperation] path=(UiPathSegment)?;

UiTypedBindableDef returns UiBindingExpression:
	{UiTypedBindableDef}
	('[' rawBindable=[UiRawBindable] (rawBindablePath=UiRawBindablePathSegment)? ']' '.' method=[cfg::UxEndpointDef]);

UiRawBindablePathSegment:
	{UiRawBindablePathSegment}
	'.' rawBindable=[UiRawBindable] (path=UiRawBindablePathSegment)?;

UiCommandBindableDef returns UiCommandBindableDef:
	{UiCommandBindableDef}
	command=UiCommand;

UiCommand returns UiCommand:
	UiMobileNavigationCommand | UiOpenDialogCommand | UiSearchWithDialogCommand;

UiMobileNavigationCommand:
	{UiMobileNavigationCommand}
	'navigateTo' targetPage=UiMobileNavigationPage;

UiOpenDialogCommand:
	{UiOpenDialogCommand}
	'openDialog' dialog=UiDialog;

UiSearchWithDialogCommand:
	{UiSearchWithDialogCommand}
	'searchWith' dialog=UiSearchDialog;

UiView returns UiView:
	UiIDEView;

UiIDEView returns UiIDEView:
	'ideview' name=ID
	'{'
	('viewset' viewSet=[UiViewSet] (';')?)?
	((beanSlots+=UiBeanSlot)
	|
	(bindingEndpointAlias+=UiBindingEndpointAlias))*
	content=UiEmbeddable ('align' contentAlignment=UiAlignment)?
	((bindingEndpointAlias+=UiBindingEndpointAlias)
	|
	(bindings+=UiBinding)
	|
	(validatorAssignments+=UiValidatorAssignment))*
	'}';

UiMobileView returns UiMobileView:
	'mobile' name=ID
	'{'
	('viewset' viewSet=[UiViewSet] (';')?)?
	((beanSlots+=UiBeanSlot)
	|
	(bindingEndpointAlias+=UiBindingEndpointAlias))*
	content=UiEmbeddable ('align' contentAlignment=UiAlignment)?
	((bindingEndpointAlias+=UiBindingEndpointAlias)
	|
	(bindings+=UiBinding)
	|
	(validatorAssignments+=UiValidatorAssignment))*
	'}';

UiI18nInfo:
	'i18n' key=STRING;

UiGridLayout returns UiGridLayout:
	{UiGridLayout}
	'gridlayout'
	('('
	(('columns=' columns=INT)?
	& (fillHorizontal?='fill-h')?
	& (fillVertical?='fill-v')?
	& (i18nInfo=UiI18nInfo)?)
	')')?
	name=ID?
	'{'
	(contents+=UiGridLayoutAssigment)*
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiVisibilityProcessorAssignment:
	{UiVisibilityProcessorAssignment}
	processor=UiVisibilityProcessor;

UiGridLayoutAssigment returns UiGridLayoutAssigment:
	element=UiEmbeddable ('align' alignment=UiAlignment)?;

UiFormLayout:
	{UiFormLayout}
	'form'
	('('
	((fillVertical?='fill-v')?
	& (fillHorizontal?='fill-h')?
	& (i18nInfo=UiI18nInfo)?)
	')')?
	name=ID?
	'{'
	(contents+=UiFormLayoutAssigment)*
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiFormLayoutAssigment:
	element=UiEmbeddable;

UiVerticalLayout:
	{UiVerticalLayout}
	'verticalLayout'
	('('
	((fillVertical?='fill-v')?
	& (i18nInfo=UiI18nInfo)?)
	')')?
	name=ID?
	'{'
	(contents+=UiVerticalLayoutAssigment)*
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiVerticalLayoutAssigment:
	element=UiEmbeddable ('align' alignment=UiAlignment)?;

UiHorizontalLayout:
	{UiHorizontalLayout}
	'horizontalLayout'
	('('
	((fillHorizontal?='fill-h')?
	& (i18nInfo=UiI18nInfo)?)
	')')?
	name=ID?
	'{'
	(contents+=UiHorizontalLayoutAssigment)*
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiHorizontalLayoutAssigment returns UiHorizontalLayoutAssigment:
	element=UiEmbeddable ('align' alignment=UiAlignment)?;

UiMobileHorizontalButtonGroup returns UiHorizontalButtonGroup:
	{UiHorizontalButtonGroup}
	'horizontalButtonGroup'
	('(' (i18nInfo=UiI18nInfo)? ')')?
	name=ID?
	'{'
	(contents+=UiMobileHorizontalButtonGroupAssigment)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiMobileHorizontalButtonGroupAssigment returns UiHorizontalButtonGroupAssigment:
	element=UiEmbeddable;

UiMobileVerticalComponentGroup returns UiVerticalComponentGroup:
	{UiVerticalComponentGroup}
	'verticalGroup'
	('(' (i18nInfo=UiI18nInfo)? ')')?
	name=ID?
	'{'
	(contents+=UiMobileVerticalComponentGroupAssigment)*
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiMobileVerticalComponentGroupAssigment returns UiVerticalComponentGroupAssigment:
	element=UiEmbeddable;

UiMobileTabSheet:
	{UiMobileTabSheet}
	'mobileTab'
	('(' (i18nInfo=UiI18nInfo)? ')')?
	name=ID?
	'{'
	(tabs+=UiMobileTabAssignment)*
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiMobileTabAssignment:
	'tab' (name=ID)? element=UiEmbeddable;

UiTabSheet:
	{UiTabSheet}
	'tabsheet'
	('(' (i18nInfo=UiI18nInfo)? ')')?
	name=ID?
	'{'
	(tabs+=UiTabAssignment)*
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiTabAssignment:
	'tab' (name=ID)? element=UiEmbeddable;

UiSplitpanel:
	'splitter' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID
	'{'
	(('first' firstContent=UiSplitpanelAssigment)
	& ('second' secondContent=UiSplitpanelAssigment)
	& ('splitPos' splitPosition=INT)?)
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiSplitpanelAssigment:
	element=UiEmbeddable;

UiPanel:
	'panel' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID
	'{'
	'content' content=UiEmbeddable
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiDialog:
	{UiDialog}
	'dialog' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID? '{'
	('type' jvmType=JvmTypeReference (';')?)?
	(content=UiDialogAssignment (';')?)?
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiDialogAssignment:
	element=UiEmbeddable;

UiSearchDialog:
	{UiSearchDialog}
	'searchdialog' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID? '{'
	('type' jvmType=JvmTypeReference)?
	('search' '{'
	searchFields+=UiDialogSearchFieldAssignment*
	'}')?
	('content' '{'
	content=UiDialogAssignment
	'}')?
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiDialogSearchFieldAssignment:
	element=UiSearchField;

UiMobileNavigationPage:
	{UiMobileNavigationPage}
	'navPage' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID? '{'
	('type' jvmType=JvmTypeReference (';')?)?
	(contents+=UiMobileNavigationPageAssignment)*
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}';

UiMobileNavigationPageAssignment:
	element=UiEmbeddable;

UiPoint returns UiPoint:
	x=INT '/' y=INT;

UiTextField returns UiTextField:
	{UiTextField}
	'textfield'
	('('
	(('maxLength=' maxLength=INT)?
	& ('minLength=' minLength=INT)?
	& ('regex=' regex=STRING)?
	& (i18nInfo=UiI18nInfo)?)
	')')?
	name=ID?
	('{'
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiSearchField:
	{UiSearchField}
	'searchfield' property=[JvmField];

UiTextArea returns UiTextArea:
	{UiTextArea}
	'textarea' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?
	('{'
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiDateField returns UiDateField:
	{UiDateField}
	'datefield' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?
	('{'
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiBrowser returns UiBrowser:
	{UiBrowser}
	'browser' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?
	('{'
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiProgressBar returns UiProgressBar:
	{UiProgressBar}
	'progressbar' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?
	('{'
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiImage returns UiImage:
	{UiImage}
	'image' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?
	('{'
	('iconPath' value=STRING (';')?)?
	(bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiList returns UiList:
	{UiList}
	'listSelect' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?;

UiTable returns UiTable:
	{UiTable}
	'table' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?
	('{'
	(('type' jvmType=JvmTypeReference (';')?)?
	& ('selectionType' selectionType=UiSelectionType (';')?)?
	& ('imageField' itemImageProperty=[JvmOperation] (';')?)?)
	columnAssignment=UiColumnsAssignment?
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiComboBox returns UiComboBox:
	{UiComboBox}
	'combo' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?
	('{'
	(('type' jvmType=JvmTypeReference (';')?)?
	& ('captionField' itemCaptionProperty=[JvmOperation] (';')?)?
	& ('imageField' itemImageProperty=[JvmOperation] (';')?)?)
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiButton:
	{UiButton}
	'button' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?;

UiMobileNavigationButton:
	{UiMobileNavigationButton}
	'navButton' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID? (targetPage=UiMobileNavigationPage | 'alias'
	targetPageAlias=[UiMobileNavigationPage]);

UiMobileSwitch returns UiSwitch:
	{UiSwitch}
	'switchIt' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID? ('{'
	(processorAssignment=UiVisibilityProcessorAssignment)?
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	'}')?;

UiLabel:
	{UiLabel}
	'label' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID? ('{'
	(processorAssignment=UiVisibilityProcessorAssignment)?
	(bindings+=UiBinding)*
	'}')?;

UiDecimalField returns UiDecimalField:
	{UiDecimalField}
	'decimalField'
	('('
	((grouping?='grouping')?
	& (markNegative?='markNegative')?
	& ('precision=' precision=INT)?
	& (i18nInfo=UiI18nInfo)?)
	')')?
	name=ID?
	('{'
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiOptionsGroup:
	{UiOptionsGroup}
	'optionsgroup' ('(' (i18nInfo=UiI18nInfo)? ')')?
	name=ID?
	('{'
	(('type' jvmType=JvmTypeReference)?
	& ('selectionType' selectionType=UiSelectionType)?
	& ('captionField' itemCaptionProperty=[JvmOperation])?
	& ('imageField' itemImageProperty=[JvmOperation])?)
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

enum UiSelectionType returns UiSelectionType:
	SINGLE='single' | MULTI='multi' | NONE='none';

enum UiAlignment:
	UNDEFINED="undefined"
	| BOTTOM_CENTER="bottom-center" | BOTTOM_LEFT="bottom-left" | BOTTOM_RIGHT="bottom-right" | BOTTOM_FILL="bottom-fill"
	| MIDDLE_CENTER="middle-center" | MIDDLE_LEFT="middle-left" | MIDDLE_RIGHT="middle-right" | MIDDLE_FILL="middle-fill"
	| TOP_CENTER="top-center" | TOP_LEFT="top-left" | TOP_RIGHT="top-right" | TOP_FILL="top-fill"
	| FILL_CENTER="fill-center" | FILL_LEFT="fill-left" | FILL_RIGHT="fill-right" | FILL_FILL="fill-fill";

UiColumnsAssignment:
	{UiColumnsAssignment}
	'columns' '{'
	columns+=UiColumn*
	'}';

UiColumn returns UiColumn:
	{UiColumn}
	'column' getter=[JvmOperation] ('icon' iconName=STRING)? (';')?;

UiEmbeddable:
	UiLayout | UiField | UiAction | UiTabSheet | UiMobileEmbeddable;

UiMobileEmbeddable:
	UiMobileLayout | UiMobileField | UiMobileTabSheet | UiMobileAction;

UiMobileLayout:
	UiMobileHorizontalButtonGroup | UiMobileVerticalComponentGroup | UiMobileNavigationPage;

UiMobileField:
	UiMobileSwitch;

UiLayout:
	UiGridLayout | UiFormLayout | UiHorizontalLayout | UiVerticalLayout | UiSplitpanel | UiPanel;

UiField:
	UiTextField | UiList | UiNumericField | UiCheckBox | UiTable | UiComboBox | UiImage | UiSearchField | UiLabel |
	UiDecimalField |
	UiTextArea | UiDateField | UiBrowser | UiProgressBar | UiOptionsGroup;

UiAction:
	UiButton;

UiMobileAction:
	UiMobileNavigationButton;

UiNumericField:
	{UiNumericField}
	'numericField'
	('('
	((grouping?='grouping')?
	& (markNegative?='markNegative')?
	& (i18nInfo=UiI18nInfo)?)
	')')?
	name=ID?
	('{'
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiCheckBox:
	{UiCheckBox}
	'checkbox' ('(' (i18nInfo=UiI18nInfo)? ')')? name=ID?
	('{'
	(validators+=UiValidator
	|
	bindings+=UiBinding)*
	(processorAssignment=UiVisibilityProcessorAssignment)?
	'}')?;

UiValidator:
	UiMaxLengthValidator | UiMinLengthValidator | UiRegexpValidator | UiXbaseValidator;

UiMaxLengthValidator returns UiMaxLengthValidator:
	{UiMaxLengthValidator}
	'MaxLengthValidator' (name=ID)? '(' maxLength=INT ')'?;

UiMinLengthValidator returns UiMinLengthValidator:
	{UiMinLengthValidator}
	'MinLengthValidator' (name=ID)? '(' minLength=INT ')'?;

UiRegexpValidator returns UiRegexpValidator:
	{UiRegexpValidator}
	'RegexValidator' (name=ID)? '(' regExpression=STRING ')'?;

UiXbaseValidator returns UiXbaseValidator:
	{UiXbaseValidator}
	'Expression' name=ID? '(' jvmType=JvmTypeReference ')' expression=XBlockExpression?;

UiVisibilityProcessor:
	{UiVisibilityProcessor}
	'visibility' (name=ID)? '{'
	propertiesAssignment=UiVisibilityPropertiesAssignment
	'}'
	('uses' '{'
	(bindingAlias+=UiBindingEndpointAlias
	|
	changeTrigger+=UiChangeTrigger)*
	'}')?
	('activation'
	rule=UiXbaseVisibilityRule)?;

UiXbaseVisibilityRule:
	expression=XBlockExpression;

UiVisibilityPropertiesAssignment:
	{UiVisibilityPropertiesAssignment}
	properties+=UiVisibilityProperty*;

UiVisibilityProperty:
	bindableDef=UiTypedBindableDef '=' assignmentExpression=XAssignment;

UiChangeTrigger:
	'fireOn' endpoint=UiBindingEndpointAssignment;


