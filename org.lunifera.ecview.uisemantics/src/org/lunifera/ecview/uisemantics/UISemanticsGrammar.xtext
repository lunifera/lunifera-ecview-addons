grammar org.lunifera.ecview.uisemantics.UISemanticsGrammar with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/xbase/Xtype"
import "http://www.eclipse.org/xtext/common/JavaVMTypes"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/org.lunifera.ecview.semantic.uisemantics/model/UISemanticsGrammar.ecore" 

UxModel:
	importSection=XImportSection?
	elementDefinitions+=UxElementDefinition*;

XImportDeclaration returns UxImportDeclaration:
	('import' ((static?='static' extension?='extension'? importedType=[JvmDeclaredType|QualifiedNameInStaticImport]
	(wildcard?='*' | memberName=ValidID))
	| importedType=[JvmDeclaredType|QualifiedName]
	| importedNamespace=QualifiedNameWithWildcard))
	| (("ePackage" importedEPackage=UxEPackageImport)) ';'?;
 
UxEPackageImport returns UxEPackageImport:
	ePackage=[ecore::EPackage|STRING] 'as' alias=ID;
 
UxElementDefinition:
	'element' name=ID '{'
	(uri=UxElementURI)?
	'bindings' '{'
	bindingContainer=UxAvailableBindings
	'}'
	'}';

UxElementURI:
	'uri' ePackage=[UxEPackageImport] ":" eClass=[ecore::EClass];

UxAvailableBindings:
	bindings+=UxEndpointDef*;

UxEndpointDef:
	UxValueBindingEndpointDef | UxListBindingEndpointDef | UxSetBindingEndpointDef;

UxValueBindingEndpointDef:
	(final?='final')? 'valueEndpoint' name=ID jvmType=JvmTypeReference;

UxListBindingEndpointDef:
	'listEndpoint' name=ID jvmType=JvmTypeReference;

UxSetBindingEndpointDef:
	'setEndpoint' name=ID jvmType=JvmTypeReference;
