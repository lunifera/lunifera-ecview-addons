grammar org.lunifera.ecview.uisemantics.UISemanticsGrammar with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/xbase/Xtype" as type
import "http://www.eclipse.org/xtext/common/JavaVMTypes"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.lunifera.org/ecview/dsl/uimodel/semantics" as semantic

UxModel returns semantic::UxModel:
	importSection=XImportSection?
	elementDefinitions+=UxElementDefinition*;

XImportDeclaration returns semantic::UxImportSectionDeclaration:
	{semantic::UxImportSectionDeclaration}
	('import' ((static?='static' extension?='extension'? importedType=[JvmDeclaredType|QualifiedNameInStaticImport]
	(wildcard?='*' | memberName=ValidID))
	| importedType=[JvmDeclaredType|QualifiedName]
	| importedNamespace=QualifiedNameWithWildcard))
	| (("ePackage" importedEPackage=UxEPackageImport)) ';'?;

UxEPackageImport returns semantic::UxEPackageImport:
	ePackage=[ecore::EPackage|STRING] 'as' alias=ID;

UxElementDefinition returns semantic::UxElementDefinition:
	'element' name=ID '{'
	(uri=UxElementURI)?
	('bindings' '{'
	bindingContainer=UxAvailableBindings
	'}')?
	('visibility' '{'
	visibilityContainer=UxAvailableVisibilityOptions
	'}')?
	('validator' '{'
		validatorContainer=UxAvailableValidatorProperties
	'}')?
	
	'}';

UxAvailableVisibilityOptions returns semantic::UxAvailableVisibilityOptions:
	{semantic::UxAvailableVisibilityOptions}
	options+=UxVisibilityOption*;

UxVisibilityOption returns semantic::UxVisibilityOption:
	'endpoint' name=ID jvmType=JvmTypeReference;

UxAvailableValidatorProperties returns semantic::UxAvailableValidatorProperties:
	{semantic::UxAvailableValidatorProperties}
	properties+=UxValidatorProperty*;

UxValidatorProperty returns semantic::UxValidatorProperty:
	'property' name=ID;

UxElementURI returns semantic::UxElementURI:
	'uri' ePackage=[semantic::UxEPackageImport] ":" eClass=[ecore::EClass];

UxAvailableBindings returns semantic::UxAvailableBindings:
	{semantic::UxAvailableBindings}
	bindings+=UxEndpointDef*;

UxEndpointDef returns semantic::UxEndpointDef:
	UxValueBindingEndpointDef;

UxValueBindingEndpointDef returns semantic::UxValueBindingEndpointDef:
	(final?='final')? (listbinding?='list')? 'endpoint' name=ID jvmType=JvmTypeReference;

//UxListBindingEndpointDef returns semantic::UxListBindingEndpointDef:
//	(final?='final')? 'listEndpoint' name=ID jvmType=JvmTypeReference;

//UxSetBindingEndpointDef returns semantic::UxSetBindingEndpointDef:
//	(final?='final')? 'setEndpoint' name=ID jvmType=JvmTypeReference;
